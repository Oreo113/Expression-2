@persist Commands:table Prefix:string

if (first()) {
    Prefix = "!"
    Commands = table()
}

function registerCommand(Name:string, Callback:string) {
    Commands[Name, string] = Callback
}

function setPrefix(NewPrefix:string) {
    Prefix = NewPrefix
}

event chat(Player:entity, Message:string, Team:number) {
    array(Team) # dispose of the team argument so that we dont get the silly warning
    if (Player != owner()) { exit() } # only the owner should be allowed to run the commands
    
    local MessagePrefix = Message:sub(1, 1)
    if (MessagePrefix != Prefix) { exit() } # If the prefix is not what we expect, stop here
    hideChat(1)
    
    local Message = Message:sub(2) # Remove the prefix from the message
    local Arguments = Message:explode(" ") # Split the command by spaces
    
    local Command = Arguments[1, string]
    Arguments:remove(1) # remove the initial command so that we just have the rest of the arguments
    
     if (Commands:exists(Command)) {
        try {
            Commands[Command, string](Player, Arguments)
        } catch (Error) {
            print(format("Failed to run callback '%s'\nError: %s", Command, Error))
        }
    } else {
        print(format("Command '%s' not found!\nAvailable commands:\n%s", Command, Commands:keys():concat(",\n")))
    }
}
